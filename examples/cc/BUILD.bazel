load("@bazel_skylib//rules:build_test.bzl", "build_test")
load(
    "@rules_build_error//lang/cc:defs.bzl",
    "cc_build_error",
    "cc_build_error_test",
    "inline_src",
)
load("@rules_build_error//matcher:defs.bzl", "matcher")

# `cc_build_error` targets are designed to build successfully
# only when the expected compilation or linking error occurs.

cc_build_error(
    name = "compile_error",
    src = "compile_error.cpp",
    compile_stderr = matcher.has_substr("Example compile error"),
)

cc_build_error(
    name = "link_error",
    src = "link_error.cpp",
    link_stderr = matcher.contains_basic_regex("Declared.*Undefined"),
)

build_test(
    name = "build_test",
    targets = [
        ":compile_error",
        ":link_error",
    ],
)

# `cc_build_error_test` is a test rule for `cc_build_error` that succeeds
# only when `cc_build_error` builds successfully,
# meaning the expected compilation or linking error occurs.

cc_build_error_test(
    name = "compile_error_test",
    src = "compile_error.cpp",
    compile_stderr = matcher.has_substr("Example compile error"),
)

cc_build_error_test(
    name = "link_error_test",
    src = "link_error.cpp",
    link_stderr = matcher.contains_basic_regex("Declared.*Undefined"),
)

# With `inline_src`, you can also provide the source as an inline string.

cc_build_error_test(
    name = "compile_error_test_with_inline_c",
    src = inline_src.c("""
    #include <assert.h>

    int main(void) {
        static_assert(0, "Compile error message for inline src");
        return 0;
    }
    """),
    compile_stderr = matcher.has_substr("static assertion failed"),
)

cc_build_error_test(
    name = "compile_error_test_with_inline_cpp",
    src = inline_src.cpp("""
    constexpr bool Predicate() noexcept { return false; }

    int main() {
        static_assert(Predicate(), "Compile error message for inline src");
        return 0;
    }
    """),
    compile_stderr = matcher.has_substr("static assertion failed"),
)

cc_build_error_test(
    name = "link_error_test_with_inline_c",
    src = inline_src.c("""
    int DeclaredButUndefined();
    int main(void) {
        return DeclaredButUndefined();
    }
    """),
    link_stderr = matcher.has_substr("DeclaredButUndefined"),
)

cc_build_error_test(
    name = "link_error_test_with_inline_cpp",
    src = inline_src.cpp("""
    int DeclaredButUndefined();
    int main() {
        return DeclaredButUndefined();
    }
    """),
    link_stderr = matcher.has_substr("DeclaredButUndefined"),
)
